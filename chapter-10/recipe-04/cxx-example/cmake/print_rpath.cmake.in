set(STAGED_INSTALL_PREFIX @STAGED_INSTALL_PREFIX@)
set(INSTALL_BINDIR @INSTALL_BINDIR@)
set(PRINT_SCRIPT @PRINT_SCRIPT@)

# Look for patchelf, chrpath, otool
set(_patcher)
list(APPEND _patchers chrpath patchelf otool)
foreach(p IN LISTS _patchers)
  find_program(${p}_FOUND
    NAMES
      ${p}
    )
  if(${p}_FOUND)
    set(_patcher ${p})
    message(STATUS "ELF patching tool ${_patcher} FOUND")
    break()
  endif()
endforeach()

if(NOT _patcher)
  message(FATAL_ERROR "ELF patching tool NOT FOUND!\nPlease install one of patchelf, chrpath or otool")
else()
  set(_elfobj ${CMAKE_INSTALL_PREFIX}/${INSTALL_BINDIR}/use_message)

  find_package(PythonInterp REQUIRED QUIET)
  execute_process(
    COMMAND
      ${PYTHON_EXECUTABLE} ${PRINT_SCRIPT} "${_patcher}" "${_elfobj}"
    RESULT_VARIABLE _res
    OUTPUT_VARIABLE _out
    ERROR_VARIABLE _err
    OUTPUT_STRIP_TRAILING_WHITESPACE
    )

  if(_res EQUAL 0)
    message(STATUS "RPATH for ${_elfobj} is ${_out}")
  else()
    message(STATUS "Something went wrong!")
    message(STATUS "Standard output from print_rpath.py: ${_out}")
    message(STATUS "Standard error from print_rpath.py: ${_err}")
    message(FATAL_ERROR "${_patcher} could NOT obtain RPATH for ${_elfobj}")
  endif()
endif()
